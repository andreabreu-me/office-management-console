buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.moowork.gradle:gradle-grunt-plugin:0.6"
    }
}

apply plugin: 'com.moowork.grunt'
apply plugin: 'java'
apply plugin: 'maven'

defaultTasks 'install'

task zipDist(type: Zip, dependsOn: grunt_release) {
    destinationDir file("${buildDir}/distributions")
    archiveName 'office-management-client.zip'
    from ('dist/')
    into 'static' // note that this specifies path *in* the archive
}

artifacts {
    archives zipDist
}

def angularSrcDir = new File( projectDir, 'src/' )
def angularVendorDir = new File( projectDir, 'vendor/' )
def gruntOutputDir = new File( projectDir, 'dist/' )

tasks
    .findAll { it.name.startsWith('grunt_') }
    .each { task -> 
        task.inputs.dir angularSrcDir
        task.inputs.dir angularVendorDir
        task.outputs.dir gruntOutputDir
        task.dependsOn 'installGrunt'
    }

// runs "grunt release" as part of your gradle build
build.dependsOn grunt_release
